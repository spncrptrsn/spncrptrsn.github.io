INIT
CHARACTER:__Me
STATUS:%ABST_AURA
STATUS:%ABST_AURA2=null
INT:%AuraOn
FLOAT:%AuraRadius

EVENTS

EVENT AuraSkillMedusaHead
ON
	OnCharacterStatusApplied(__Me,MEDUSA_HEAD)
ACTIONS
	Set(%AuraOn,1)
	Set(%ABST_AURA,OVR_PETRIFIED)
	Set(%AuraRadius,3.0)
	StartTimer("AuraActivate",.25,0)
	
EVENT AuraSkillBlindingRadiance
ON
	OnCharacterStatusApplied(__Me,BLINDING_RADIANCE)		
ACTIONS
	Set(%AuraOn,1)
	Set(%ABST_AURA,OVR_BLIND)
	Set(%AuraRadius,5.0)
	StartTimer("AuraActivate",.25,0)

EVENT AuraSkillVacuumAura
ON
	OnCharacterStatusApplied(__Me,VACUUM_AURA)
ACTIONS
	Set(%AuraOn,1)
	Set(%ABST_AURA,OVR_MUTED)	
	Set(%AuraRadius,8.0)
	StartTimer("AuraActivate",.25,0)

EVENT AuraSkillQuestSilenceAura
ON
	OnCharacterStatusApplied(__Me,QUEST_SILENCE_AURA)
ACTIONS
	Set(%AuraOn,1)
	Set(%ABST_AURA,OVR_MUTED)
	Set(%AuraRadius,20.0)
	StartTimer("AuraActivate",.25,0)

EVENT AuraSkillQuestFearAura
ON
	OnCharacterStatusApplied(__Me,QUEST_FEAR_AURA)
ACTIONS
	Set(%AuraOn,1)
	Set(%ABST_AURA,OVR_FEAR)
	Set(%AuraRadius,20.0)
	StartTimer("AuraActivate",.25,0)

EVENT AuraSkillQuestPoisonAura
ON
	OnCharacterStatusApplied(__Me,QUEST_POISON_AURA)
ACTIONS
	Set(%AuraOn,1)
	Set(%ABST_AURA,OVR_POISONED)
	Set(%AuraRadius,20.0)
	StartTimer("AuraActivate",.25,0)
	
EVENT AuraSkillInfectiousDisease
ON
	OnCharacterStatusApplied(__Me,OVR_INFECTIOUS_DISEASED)
ACTIONS
	Set(%AuraOn,1)
	Set(%ABST_AURA,OVR_DISEASED)
	Set(%AuraRadius,20.0)
	StartTimer("InfectAuraActivate",.25,0)	

EVENT AuraTurn
ON
	OnTurn(__Me,_) 
ACTIONS
	IF "c1"
		IsEqual(%AuraOn,1)
	THEN
		StartTimer("AuraActivate",.75,0)
	ENDIF	
	
EVENT AuraTurnOver
ON
	OnTurnEnded(__Me,_)
ACTIONS
	IF "c1"
		IsEqual(%AuraOn,1)
	THEN
		StartTimer("AuraActivate",.1,0)
	ENDIF	
			
EVENT ProcAura
ON
	OnTimer("AuraActivate")
ACTIONS
	IterateCharactersNear(__Me,%AuraRadius,"AuraCast")
	
EVENT ProcInfectAura
ON
	OnTimer("InfectAuraActivate")
ACTIONS
	IterateCharactersNear(__Me,%AuraRadius,"InfectCast")	

EVENT AuraCast
VARS
CHARACTER:_Char
ON
	OnIterateCharacter(_Char,"AuraCast")
ACTIONS
	IF "c1"
		CharacterIsEnemy(__Me,_Char)
	THEN	
		CharacterApplyStatus(_Char,%ABST_AURA,1,0,__Me)
	ENDIF	
	IF "c1"
		CharacterIsEnemy(__Me,_Char)
	THEN	
		CharacterApplyStatus(_Char,%ABST_AURA2,1,0,__Me)
	ENDIF		
	
EVENT InfectCast
VARS
CHARACTER:_Char
ON
	OnIterateCharacter(_Char,"InfectCast")
ACTIONS
	IF "!c1"
		IsEqual(_Char,__Me)
	THEN
		CharacterApplyStatus(_Char,%ABST_AURA,2,0,__Me)
	ENDIF
	
EVENT AuraSkillRemoved
ON
	OnCharacterStatusRemoved(__Me,MEDUSA_HEAD)
	OnCharacterStatusRemoved(__Me,BLINDING_RADIANCE)
	OnCharacterStatusRemoved(__Me,VACUUM_AURA)
	OnCharacterStatusRemoved(__Me,QUEST_SILENCE_AURA)
	OnCharacterStatusRemoved(__Me,QUEST_FEAR_AURA)
	OnCharacterStatusRemoved(__Me,QUEST_POISON_AURA)
	OnCharacterStatusRemoved(__Me,OVR_INFECTIOUS_DISEASED)	
ACTIONS
	Set(%AuraOn,0)
	Set(%ABST_AURA2,null)