INIT
EVENTS

INIT
CHARACTER:__Me
CHARACTER:%ContactCaster
CHARACTER:%Contacted
INT:%DeflectInt=0
INT:%UnstableCounter=0
INT:%TimerTicker=0
INT:%DualCounter=0
INT:%HighestRoll=100
INT:%WeaponStatus=0
STRING:%AltStatusString=null
STRING:%StatusString=""
STRING:%CombatLogText1
STRING:%CombatLogText2
STRING:%CombatLogText3
STRING:%CombatLogText4
STRING:%CombatLogText5
STATUS:%ScrubbedStatus=null
STATUS:%ContactStatus
STATUS:%RemovedStatus
EVENTS

//===========================================================//
//                   Character Extensions                   *//
//===========================================================//

EVENT ABSTStartUp
VARS
CHARACTER:_Dummy
ON
	OnInit()
	OnLoaded(_,_,_,_)
ACTIONS
	CharacterCharacterEvent(__Me,_Dummy,"ABST_NameMe")
	IF "c1&!c2"
		CharacterIsInParty(__Me)
		CharacterHasSkill(__Me,Target_DeflectPreview)
	THEN
		CharacterAddSkill(__Me,Target_DeflectPreview)
	ENDIF

EVENT ABSTNonCombatSkill
VARS
CHARACTER:_Char
ON
	OnSkillCast(_Char,_)
ACTIONS
	IF "c1&c2&!c3"
		IsInCombat(__Me)
		CharacterIsInParty(_Char)
		IsInActiveTurn(_Char)
	THEN
		CharacterRemoveStatus(__Me,CASTER_NONSURFACE,null,0)
		CharacterApplyStatus(__Me,CASTER_NONSURFACE,1,0,_Char)
		Set(%ScrubbedStatus,CASTER_NONSURFACE)
		StartTimer("StatusRemovalTimer",2,0)
		IF "c1"
			CharacterHasStatus(__Me,CASTER_AURA)
		THEN
			CharacterRemoveStatus(__Me,CASTER_AURA,null,0)
			StartTimer("ApplyCasterStatus",3,0)
		ENDIF
	ELSE
	CharacterRemoveStatus(__Me,CASTER_SURFACE,null,0)
	ENDIF

EVENT ABSTOnCombat
VARS
CHARACTER:_Dummy
ON
	OnCombatStarted(_)
ACTIONS
	IF "c1"
		CharacterHasTalent(__Me,ResurrectToFullHealth)
	THEN
		CharacterApplyStatus(__Me,ABST_MORNING,-1)
	ENDIF
	CharacterCharacterEvent(__Me,_Dummy,"ABST_NameMe")
	IF "c1&!c2"
		CharacterIsInParty(__Me)
		CharacterHasSkill(__Me,Target_DeflectPreview)
	THEN
		CharacterAddSkill(__Me,Target_DeflectPreview)
	ENDIF
	IF "c1"
		IsTagged(__Me,"ABST_BOSS")
	THEN
		SetIsBoss(__Me,1)
	ENDIF

EVENT ABSTAnyTurn
VARS
FLOAT:_Vitality
CHARACTER:_Dummy
ON
	OnTurn(_,_)
ACTIONS
	StartTimer("RemoveCasterStatus",.3,0)
	IF "c1"
		IsInActiveTurn(__Me)
	THEN
		StartTimer("ApplyCasterStatus",0.5,0)
	ENDIF

EVENT ABSTMyTurn
VARS
INT:_Int
FLOAT:_Vitality
ON
	OnTurn(__Me,_)
ACTIONS
	CharacterRemoveStatus(__Me,ABST_AEGIS,null,0)
	IF "c1&c2"
		IsInSurface(__Me,SurfaceSmokeCloud)
		CharacterHasTalent(__Me,SurpriseAttack)
	THEN
		CharacterApplyStatus(__Me,ABST_GUERILLA,-1)
	ENDIF
	IF "c1&c2&c3"
		CharacterHasTalent(__Me,WhatARush)
		CharacterGetStat(_Vitality,__Me,Vitality)
		IsLessThen(_Vitality,.5)
	THEN
		CharacterApplyStatus(__Me,ABST_RUSH,-1)
	ENDIF
	Set(%DualCounter,0)

EVENT ABSTAttack
VARS
CHARACTER:_Char
ON
	OnCharacterStartAttackObject(_Char,_,_,__Me)
ACTIONS
	IF "c1&c2&(c3|(c4&!c5&!c6))"
		CharacterHasTalent(__Me,DualWieldingDodging)
		IsInCombat(__Me)
		CharacterHasSkill(__Me,Target_DualWieldingAttack)
		CharacterHasWeaponType(__Me,Wand,1)
		CharacterHasSkill(__Me,Target_SingleHandedAttack)
		CharacterHasSkill(__Me,Shout_RecoverArmour)
	THEN
		StartTimer("ParryMasterMomentum",1.5,0)
	ENDIF
	IF "!c1&!c2&!c3"
		CharacterHasWeaponType(__Me,Bow,1)
		CharacterHasWeaponType(__Me,Crossbow,1)
		CharacterHasWeaponType(__Me,Wand,1)
	THEN
		Set(%Contacted,__Me)
		Set(%ContactCaster,_Char)
	ENDIF

EVENT ABSTMiss
VARS
CHARACTER:_Char
ON
	OnMiss(_Char,_,__Me,_)
ACTIONS
	CharacterApplyStatus(__Me,ABST_MISSED,1)
	Set(%ScrubbedStatus,ABST_MISSED)
	StartTimer("StatusRemovalTimer",5,0)

EVENT ABSTTurnEnd
ON
	OnTurnEnded(__Me,_)
ACTIONS
	StartTimer("RemoveTalentStuff",1.5,0)

EVENT ABSTOnCombatEnded
ON
	OnLeftCombat(__Me,_)
ACTIONS
	CharacterRemoveStatus(__Me,ABST_MORNING,null,0)
	CharacterRemoveStatus(__Me,ABST_GUERILLA,null,0)
	CharacterRemoveStatus(__Me,ABST_EXEC,null,0)
	CharacterRemoveStatus(__Me,ABST_RUSH,null,0)
	CharacterRemoveStatus(__Me,ABST_DUAL5,null,0)
	CharacterRemoveStatus(__Me,ABST_DUAL10,null,0)
	CharacterRemoveStatus(__Me,ABST_DUAL15,null,0)
	CharacterRemoveStatus(__Me,ABST_DUAL20,null,0)
	CharacterRemoveStatus(__Me,ABST_DUAL25,null,0)
	
